apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'

version = libVersion
group = groupId


// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    publish = true
    override = false
    pkg {
        repo = 'maven'
        name = pkg_name
        version {
            name = libVersion
            released = new Date()
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId groupId
                artifactId artifactId
                version libVersion
                name artifactId
                description libraryDescription
                url siteUrl
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


//just copy paste the lines below
def isAndroid = project.hasProperty("android")
def isKotlin = project.hasProperty("kotlin")
def isJava = project.hasProperty("Java")

if (isKotlin) {
    //Kotlin libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        if (isAndroid) from android.sourceSets.main.java.srcDirs else from sourceSets.main.java.srcDirs
    }
    if (isAndroid) task javadoc(type: Javadoc, dependsOn: dokka) {}
} else if (isAndroid) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else {
    // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    //  options.encoding = 'UTF-8'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}